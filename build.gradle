defaultTasks 'build' // just an incremental compile with tests.
task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}
buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }
    dependencies {
        // for adding git data to jar manifests.
        classpath 'gradle.plugin.org.dvaske.gradle:git-build-info:0.2'
        // This plugin is needed because all the akka deps have their own reference.conf and if the right one doesn't get loaded akka fails.
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'gradle.plugin.com.github.maiflai:gradle-scalatest:0.23'
    }
}

// plugins {
//   id "com.github.maiflai.scalatest" version "0.23"
// }
apply plugin: 'com.github.maiflai.scalatest'
apply plugin: 'org.dvaske.gradle.git-build-info'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    // local libs
    flatDir {
        dirs "${rootProject.projectDir}/libs"
    }
}
apply plugin: 'scala'
group = 'rdb4s'
version = '1.0'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
jar {
    baseName = rootProject.name
    appendix = project.name
    manifest {
        attributes('SHA': gitHead, 'DESC': gitDescribeInfo)
    }
}
tasks.withType(ScalaCompile) {
    dependencies {
        compile 'org.scala-lang:scala-library:2.12.4'
        compile 'org.scala-lang:scala-compiler:2.12.4'
        testCompile 'org.scalatest:scalatest_2.12:3.0.4'
		testRuntime 'org.pegdown:pegdown:1.4.2'
    }
    configure(scalaCompileOptions) {
        listFiles = false
        unchecked = true
        deprecation = true
        // [2018-8-2] IntelliJ doesn't pick these up, it seems, so don't put anything in here that isn't covered in 
        // File | Settings | Build, ... | Compiler | Scala Compiler | Options
        additionalParameters = [
                '-unchecked',
                '-deprecation',
                '-feature',
                '-encoding', 'utf8']
    }
}
// Makes slightly less noise in the output.
compileJava.enabled = false
dependencies {
	testCompile group: 'com.codahale.metrics', name: 'metrics-core', version: '3.0.2'
	testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'
}
